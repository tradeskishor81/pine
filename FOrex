// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// ©jdehorty

// @version=5
indicator('Machine Learning: Lorentzian Classification', 'Lorentzian Classification', true, precision=4, max_labels_count=500) 

import jdehorty/MLExtensions/2 as ml
import jdehorty/KernelFunctions/2 as kernels

// ====================
// ==== Background ====
// ====================

// When using Machine Learning algorithms like K-Nearest Neighbors, choosing an
// appropriate distance metric is essential. Euclidean Distance is often used as
// the default distance metric, but it may not always be the best choice. This is
// because market data is often significantly impacted by proximity to significant
// world events such as FOMC Meetings and Black Swan events. These major economic
// events can contribute to a warping effect analogous a massive object's 
// gravitational warping of Space-Time. In financial markets, this warping effect 
// operates on a continuum, which can analogously be referred to as "Price-Time".

// To help to better account for this warping effect, Lorentzian Distance can be
// used as an alternative distance metric to Euclidean Distance. The geometry of
// Lorentzian Space can be difficult to visualize at first, and one of the best
// ways to intuitively understand it is through an example involving 2 feature
// dimensions (z=2). For purposes of this example, let's assume these two features
// are Relative Strength Index (RSI) and the Average Directional Index (ADX). In
// reality, the optimal number of features is in the range of 3-8, but for the sake
// of simplicity, we will use only 2 features in this example.

// Fundamental Assumptions:
// (1) We can calculate RSI and ADX for a given chart.
// (2) For simplicity, values for RSI and ADX are assumed to adhere to a Gaussian 
//     distribution in the range of 0 to 100.
// (3) The most recent RSI and ADX value can be considered the origin of a coordinate 
//     system with ADX on the x-axis and RSI on the y-axis.

// Distances in Euclidean Space:
// Measuring the Euclidean Distances of historical values with the most recent point
// at the origin will yield a distribution that resembles Figure 1 (below).

//                        [RSI]
//                          |                      
//                          |                   
//                          |                 
//                      ...:::....              
//                .:.:::••••••:::•::..             
//              .:•:.:•••::::••::••....::.            
//             ....:••••:••••••••::••:...:•.          
//            ...:.::::::•••:::•••:•••::.:•..          
//            ::•:.:•:•••••••:.:•::::::...:..         
//  |--------.:•••..•••••••:••:...:::•:•:..:..----------[ADX]    
//  0        :•:....:•••••::.:::•••::••:.....            
//           ::....:.:••••••••:•••::••::..:.          
//            .:...:••:::••••••••::•••....:          
//              ::....:.....:•::•••:::::..             
//                ..:..::••..::::..:•:..              
//                    .::..:::.....:                
//                          |            
//                          |                   
//                          |
//                          |
//                         _|_ 0        
//                          
//        Figure 1: Neighborhood in Euclidean Space

// Distances in Lorentzian Space:
// However, the same set of historical values measured using Lorentzian Distance will 
// yield a different distribution that resembles Figure 2 (below).

//                         
//                         [RSI] 
//  ::..                     |                    ..:::  
//   .....                   |                  ......
//    .••••::.               |               :••••••. 
//     .:•••••:.             |            :::••••••.  
//       .•••••:...          |         .::.••••••.    
//         .::•••••::..      |       :..••••••..      
//            .:•••••••::.........::••••••:..         
//              ..::::••••.•••••••.•••••••:.            
//                ...:•••••••.•••••••••::.              
//                  .:..••.••••••.••••..                
//  |---------------.:•••••••••••••••••.---------------[ADX]          
//  0             .:•:•••.••••••.•••••••.                
//              .••••••••••••••••••••••••:.            
//            .:••••••••••::..::.::••••••••:.          
//          .::••••••::.     |       .::•••:::.       
//         .:••••••..        |          :••••••••.     
//       .:••••:...          |           ..•••••••:.   
//     ..:••::..             |              :.•••••••.   
//    .:•....                |               ...::.:••.  
//   ...:..                  |                   :...:••.     
//  :::.                     |                       ..::  
//                          _|_ 0
//
//       Figure 2: Neighborhood in Lorentzian Space 


// Observations:
// (1) In Lorentzian Space, the shortest distance between two points is not 
//     necessarily a straight line, but rather, a geodesic curve.
// (2) The warping effect of Lorentzian distance reduces the overall influence  
//     of outliers and noise.
// (3) Lorentzian Distance becomes increasingly different from Euclidean Distance 
//     as the number of nearest neighbors used for comparison increases.

// ======================
// ==== Custom Types ====
// ======================

// This section uses PineScript's new Type syntax to define important data structures
// used throughout the script.

type Settings
    float source
    int neighborsCount
    int maxBarsBack
    int featureCount
    int colorCompression
    bool showExits
    bool useDynamicExits
    int exitAfterBars
    bool exitOnNeutral

type Label
    int long
    int short
    int neutral

type FeatureArrays
    array<float> f1
    array<float> f2
    array<float> f3
    array<float> f4
    array<float> f5

type FeatureSeries
    float f1
    float f2
    float f3
    float f4
    float f5

type MLModel
    int firstBarIndex
    array<int> trainingLabels
    int loopSize
    float lastDistance
    array<float> distancesArray
    array<int> predictionsArray
    int prediction

type FilterSettings 
    bool useVolatilityFilter
    bool useRegimeFilter
    bool useAdxFilter
    float regimeThreshold
    int adxThreshold

type Filter
    bool volatility
    bool regime
    bool adx 

// ==========================
// ==== Helper Functions ====
// ==========================

series_from(feature_string, _close, _high, _low, _hlc3, f_paramA, f_paramB) =>
    switch feature_string
        "RSI" => ml.n_rsi(_close, f_paramA, f_paramB)
        "WT" => ml.n_wt(_hlc3, f_paramA, f_paramB)
        "CCI" => ml.n_cci(_close, f_paramA, f_paramB)
        "ADX" => ml.n_adx(_high, _low, _close, f_paramA)

get_lorentzian_distance(int i, int featureCount, FeatureSeries featureSeries, FeatureArrays featureArrays) =>
    switch featureCount
        5 => math.log(1+math.abs(featureSeries.f1 - array.get(featureArrays.f1, i))) + 
             math.log(1+math.abs(featureSeries.f2 - array.get(featureArrays.f2, i))) + 
             math.log(1+math.abs(featureSeries.f3 - array.get(featureArrays.f3, i))) + 
             math.log(1+math.abs(featureSeries.f4 - array.get(featureArrays.f4, i))) + 
             math.log(1+math.abs(featureSeries.f5 - array.get(featureArrays.f5, i)))
        4 => math.log(1+math.abs(featureSeries.f1 - array.get(featureArrays.f1, i))) +
             math.log(1+math.abs(featureSeries.f2 - array.get(featureArrays.f2, i))) +
             math.log(1+math.abs(featureSeries.f3 - array.get(featureArrays.f3, i))) +
             math.log(1+math.abs(featureSeries.f4 - array.get(featureArrays.f4, i)))
        3 => math.log(1+math.abs(featureSeries.f1 - array.get(featureArrays.f1, i))) +
             math.log(1+math.abs(featureSeries.f2 - array.get(featureArrays.f2, i))) +
             math.log(1+math.abs(featureSeries.f3 - array.get(featureArrays.f3, i)))
        2 => math.log(1+math.abs(featureSeries.f1 - array.get(featureArrays.f1, i))) +
             math.log(1+math.abs(featureSeries.f2 - array.get(featureArrays.f2, i)))

// ================  
// ==== Inputs ==== 
// ================ 

// Settings Object: General User-Defined Inputs
Settings settings = 
 Settings.new(
   input.source(title='Source', defval=close, group="General Settings", tooltip="Source of the input data"),
   input.int(title='Neighbors Count', defval=8, group="General Settings", minval=1, maxval=100, step=1, tooltip="Number of neighbors to consider"),
   input.int(title="Max Bars Back", defval=2000, group="General Settings"),
   input.int(title="Feature Count", defval=5, group="Feature Engineering", minval=2, maxval=5, tooltip="Number of features to use for ML predictions."),
   input.int(title="Color Compression", defval=1, group="General Settings", minval=1, maxval=10, tooltip="Compression factor for adjusting the intensity of the color scale."),
   input.bool(title="Show Default Exits", defval=false, group="General Settings", tooltip="Default exits occur exactly 4 bars after an entry signal. This corresponds to the predefined length of a trade during the model's training process.", inline="exits"),
   input.bool(title="Use Dynamic Exits", defval=false, group="General Settings", tooltip="Dynamic exits attempt to let profits ride by dynamically adjusting the exit threshold based on kernel regression logic.", inline="exits"),
   exitAfterBars,
   exitOnNeutral
 )
   
// Trade Stats Settings
// Note: The trade stats section is NOT intended to be used as a replacement for proper backtesting. It is intended to be used for calibration purposes only.
showTradeStats = input.bool(true, 'Show Trade Stats', tooltip='Displays the trade stats for a given configuration. Useful for optimizing the settings in the Feature Engineering section. This should NOT replace backtesting and should be used for calibration purposes only. Early Signal Flips represent instances where the model changes signals before 4 bars elapses; high values can indicate choppy (ranging) market conditions.', group="General Settings")
useWorstCase = input.bool(false, "Use Worst Case Estimates", tooltip="Whether to use the worst case scenario for backtesting. This option can be useful for creating a conservative estimate that is based on close prices only, thus avoiding the effects of intrabar repainting. This option assumes that the user does not enter when the signal first appears and instead waits for the bar to close as confirmation. On larger timeframes, this can mean entering after a large move has already occurred. Leaving this option disabled is generally better for those that use this indicator as a source of confluence and prefer estimates that demonstrate discretionary mid-bar entries. Leaving this option enabled may be more consistent with traditional backtesting results.", group="General Settings")
exitAfterBars = input.int(title="Exit After Bars", defval=4, minval=1, maxval=100, tooltip="Number of candles to hold a position before exiting. Replaces the hardcoded value of 4 bars.", group="General Settings")
exitOnNeutral = input.bool(title="Exit on Neutral Signal", defval=true, tooltip="When enabled, exits the trade after the specified number of bars even if the signal remains neutral.", group="General Settings")

// Settings object for user-defined settings
FilterSettings filterSettings =
 FilterSettings.new(
   input.bool(title="Use Volatility Filter", defval=true, tooltip="Whether to use the volatility filter.", group="Filters"),
   input.bool(title="Use Regime Filter", defval=true, group="Filters", inline="regime"),
   input.bool(title="Use ADX Filter", defval=false, group="Filters", inline="adx"),
   input.float(title="Threshold", defval=-0.1, minval=-10, maxval=10, step=0.1, tooltip="Whether to use the trend detection filter. Threshold for detecting Trending/Ranging markets.", group="Filters", inline="regime"),
   input.int(title="Threshold", defval=20, minval=0, maxval=100, step=1, tooltip="Whether to use the ADX filter. Threshold for detecting Trending/Ranging markets.", group="Filters", inline="adx")
 )

// Filter object for filtering the ML predictions
Filter filter =
 Filter.new(
   ml.filter_volatility(1, 10, filterSettings.useVolatilityFilter), 
   ml.regime_filter(ohlc4, filterSettings.regimeThreshold, filterSettings.useRegimeFilter),
   ml.filter_adx(settings.source, 14, filterSettings.adxThreshold, filterSettings.useAdxFilter)
  )

// Feature Variables: User-Defined Inputs for calculating Feature Series. 
f1_string = input.string(title="Feature 1", options=["RSI", "WT", "CCI", "ADX"], defval="RSI", inline = "01", tooltip="The first feature to use for ML predictions.", group="Feature Engineering")
f1_paramA = input.int(title="Parameter A", tooltip="The primary parameter of feature 1.", defval=14, inline = "02", group="Feature Engineering")
f1_paramB = input.int(title="Parameter B", tooltip="The secondary parameter of feature 2 (if applicable).", defval=1, inline = "02", group="Feature Engineering")
f2_string = input.string(title="Feature 2", options=["RSI", "WT", "CCI", "ADX"], defval="WT", inline = "03", tooltip="The second feature to use for ML predictions.", group="Feature Engineering")
f2_paramA = input.int(title="Parameter A", tooltip="The primary parameter of feature 2.", defval=10, inline = "04", group="Feature Engineering")
f2_paramB = input.int(title="Parameter B", tooltip="The secondary parameter of feature 2 (if applicable).", defval=11, inline = "04", group="Feature Engineering")
f3_string = input.string(title="Feature 3", options=["RSI", "WT", "CCI", "ADX"], defval="CCI", inline = "05", tooltip="The third feature to use for ML predictions.", group="Feature Engineering")
f3_paramA = input.int(title="Parameter A", tooltip="The primary parameter of feature 3.", defval=20, inline = "06", group="Feature Engineering")
f3_paramB = input.int(title="Parameter B", tooltip="The secondary parameter of feature 3 (if applicable).", defval=1, inline = "06", group="Feature Engineering")
f4_string = input.string(title="Feature 4", options=["RSI", "WT", "CCI", "ADX"], defval="ADX", inline = "07", tooltip="The fourth feature to use for ML predictions.", group="Feature Engineering")
f4_paramA = input.int(title="Parameter A", tooltip="The primary parameter of feature 4.", defval=20, inline = "08", group="Feature Engineering")
f4_paramB = input.int(title="Parameter B", tooltip="The secondary parameter of feature 4 (if applicable).", defval=2, inline = "08", group="Feature Engineering")
f5_string = input.string(title="Feature 5", options=["RSI", "WT", "CCI", "ADX"], defval="RSI", inline = "09", tooltip="The fifth feature to use for ML predictions.", group="Feature Engineering")
f5_paramA = input.int(title="Parameter A", tooltip="The primary parameter of feature 5.", defval=9, inline = "10", group="Feature Engineering")
f5_paramB = input.int(title="Parameter B", tooltip="The secondary parameter of feature 5 (if applicable).", defval=1, inline = "10", group="Feature Engineering")

// FeatureSeries Object: Calculated Feature Series based on Feature Variables
featureSeries = 
 FeatureSeries.new(
   series_from(f1_string, close, high, low, hlc3, f1_paramA, f1_paramB), // f1
   series_from(f2_string, close, high, low, hlc3, f2_paramA, f2_paramB), // f2 
   series_from(f3_string, close, high, low, hlc3, f3_paramA, f3_paramB), // f3
   series_from(f4_string, close, high, low, hlc3, f4_paramA, f4_paramB), // f4
   series_from(f5_string, close, high, low, hlc3, f5_paramA, f5_paramB)  // f5
 )

// FeatureArrays Variables: Storage of Feature Series as Feature Arrays Optimized for ML
// Note: These arrays cannot be dynamically created within the FeatureArrays Object Initialization and thus must be set-up in advance.
var f1Array = array.new_float()
var f2Array = array.new_float()
var f3Array = array.new_float()
var f4Array = array.new_float()
var f5Array = array.new_float()
array.push(f1Array, featureSeries.f1)
array.push(f2Array, featureSeries.f2)
array.push(f3Array, featureSeries.f3)
array.push(f4Array, featureSeries.f4)
array.push(f5Array, featureSeries.f5)

// FeatureArrays Object: Storage of the calculated FeatureArrays into a single object
featureArrays = 
 FeatureArrays.new(
  f1Array, // f1
  f2Array, // f2
  f3Array, // f3
  f4Array, // f4
  f5Array  // f5
 )

// Label Object: Used for classifying historical data as training data for the ML Model
Label direction = 
 Label.new(
   long=1, 
   short=-1, 
   neutral=0
  )

// Derived from General Settings
maxBarsBackIndex = last_bar_index >= settings.maxBarsBack ? last_bar_index - settings.maxBarsBack : 0

// EMA Settings 
useEmaFilter = input.bool(title="Use EMA Filter", defval=false, group="Filters", inline="ema")
emaPeriod = input.int(title="Period", defval=200, minval=1, step=1, group="Filters", inline="ema", tooltip="The period of the EMA used for the EMA Filter.")
isEmaUptrend = useEmaFilter ? close > ta.ema(close, emaPeriod) : true
isEmaDowntrend = useEmaFilter ? close < ta.ema(close, emaPeriod) : true
useSmaFilter = input.bool(title="Use SMA Filter", defval=false, group="Filters", inline="sma")
smaPeriod = input.int(title="Period", defval=200, minval=1, step=1, group="Filters", inline="sma", tooltip="The period of the SMA used for the SMA Filter.")
isSmaUptrend = useSmaFilter ? close > ta.sma(close, smaPeriod) : true
isSmaDowntrend = useSmaFilter ? close < ta.sma(close, smaPeriod) : true

// Nadaraya-Watson Kernel Regression Settings
useKernelFilter = input.bool(true, "Trade with Kernel", group="Kernel Settings", inline="kernel")
showKernelEstimate = input.bool(true, "Show Kernel Estimate", group="Kernel Settings", inline="kernel")
useKernelSmoothing = input.bool(false, "Enhance Kernel Smoothing", tooltip="Uses a crossover based mechanism to smoothen kernel color changes. This often results in less color transitions overall and may result in more ML entry signals being generated.", inline='1', group='Kernel Settings')
h = input.int(8, 'Lookback Window', minval=3, tooltip='The number of bars used for the estimation. This is a sliding value that represents the most recent historical bars. Recommended range: 3-50', group="Kernel Settings", inline="kernel")
r = input.float(8., 'Relative Weighting', step=0.25, tooltip='Relative weighting of time frames. As this value approaches zero, the longer time frames will exert more influence on the estimation. As this value approaches infinity, the behavior of the Rational Quadratic Kernel will become identical to the Gaussian kernel. Recommended range: 0.25-25', group="Kernel Settings", inline="kernel")
x = input.int(25, "Regression Level", tooltip='Bar index on which to start regression. Controls how tightly fit the kernel estimate is to the data. Smaller values are a tighter fit. Larger values are a looser fit. Recommended range: 2-25', group="Kernel Settings", inline="kernel")
lag = input.int(2, "Lag", tooltip="Lag for crossover detection. Lower values result in earlier crossovers. Recommended range: 1-2", inline='1', group='Kernel Settings')

// Display Settings
showBarColors = input.bool(true, "Show Bar Colors", tooltip="Whether to show the bar colors.", group="Display Settings")
showBarPredictions = input.bool(defval = true, title = "Show Bar Prediction Values", tooltip = "Will show the ML model's evaluation of each bar as an integer.", group="Display Settings")
useAtrOffset = input.bool(defval = false, title = "Use ATR Offset", tooltip = "Will use the ATR offset instead of the bar prediction offset.", group="Display Settings")
barPredictionsOffset = input.float(0, "Bar Prediction Offset", minval=0, tooltip="The offset of the bar predictions as a percentage from the bar high or close.", group="Display Settings")

// =================================
// ==== Next Bar Classification ====
// =================================

// This model specializes specifically in predicting the direction of price action over the course of the next N bars (configurable). 
// The number of bars is now user-configurable via the exitAfterBars setting.
src = settings.source
y_train_series = src[settings.exitAfterBars] < src[0] ? direction.short : src[settings.exitAfterBars] > src[0] ? direction.long : direction.neutral
var y_train_array = array.new_int(0)

// Variables used for ML Logic
var predictions = array.new_float(0)
var prediction = 0.
var signal = direction.neutral
var distances = array.new_float(0)

array.push(y_train_array, y_train_series)

// =========================
// ====  Core ML Logic  ====
// =========================

// Approximate Nearest Neighbors Search with Lorentzian Distance:
// A novel variation of the Nearest Neighbors (NN) search algorithm that ensures a chronologically uniform distribution of neighbors.

// In a traditional KNN-based approach, we would iterate through the entire dataset and calculate the distance between the current bar 
// and every other bar in the dataset and then sort the distances in ascending order. We would then take the first k bars and use their 
// labels to determine the label of the current bar. 

// There are several problems with this traditional KNN approach in the context of real-time calculations involving time series data:
// - It is computationally expensive to iterate through the entire dataset and calculate the distance between every historical bar and
//   the current bar.
// - Market time series data is often non-stationary, meaning that the statistical properties of the data change slightly over time.
// - It is possible that the nearest neighbors are not the most informative ones, and the KNN algorithm may return poor results if the
//   nearest neighbors are not representative of the majority of the data.

// Previously, the user @capissimo attempted to address some of these issues in several of his PineScript-based KNN implementations by:
// - Using a modified KNN algorithm based on consecutive furthest neighbors to find a set of approximate "nearest" neighbors.
// - Using a sliding window approach to only calculate the distance between the current bar and the most recent n bars in the dataset.

// Of these two approaches, the latter is inherently limited by the fact that it only considers the most recent bars in the overall dataset. 

// The former approach has more potential to leverage historical price action, but is limited by:
// - The possibility of a sudden "max" value throwing off the estimation
// - The possibility of selecting a set of approximate neighbors that are not representative of the majority of the data by oversampling 
//   values that are not chronologically distinct enough from one another
// - The possibility of selecting too many "far" neighbors, which may result in a poor estimation of price action

// To address these issues, a novel Approximate Nearest Neighbors (ANN) algorithm is used in this indicator.

// In the below ANN algorithm:
// 1. The algorithm iterates through the dataset in chronological order, using the modulo operator to only perform calculations every 4 bars.
//    This serves the dual purpose of reducing the computational overhead of the algorithm and ensuring a minimum chronological spacing 
//    between the neighbors of at least 4 bars.
// 2. A list of the k-similar neighbors is simultaneously maintained in both a predictions array and corresponding distances array.
// 3. When the size of the predictions array exceeds the desired number of nearest neighbors specified in settings.neighborsCount, 
//    the algorithm removes the first neighbor from the predictions array and the corresponding distance array.
// 4. The lastDistance variable is overriden to be a distance in the lower 25% of the array. This step helps to boost overall accuracy 
//    by ensuring subsequent newly added distance values increase at a slower rate.
// 5. Lorentzian distance is used as a distance metric in order to minimize the effect of outliers and take into account the warping of 
//    "price-time" due to proximity to significant economic events.

lastDistance = -1.0
size = math.min(settings.maxBarsBack-1, array.size(y_train_array)-1)
sizeLoop = math.min(settings.maxBarsBack-1, size)

if bar_index >= maxBarsBackIndex //{
    for i = 0 to sizeLoop //{
        d = get_lorentzian_distance(i, settings.featureCount, featureSeries, featureArrays) 
        if d >= lastDistance and i%4 //{
            lastDistance := d            
            array.push(distances, d)
            array.push(predictions, math.round(array.get(y_train_array, i)))
            if array.size(predictions) > settings.neighborsCount //{
                lastDistance := array.get(distances, math.round(settings.neighborsCount*3/4))
                array.shift(distances)
                array.shift(predictions)
            //}
        //}
    //}
    prediction := array.sum(predictions)
//}

// ============================
// ==== Prediction Filters ====
// ============================

// User Defined Filters: Used for adjusting the frequency of the ML Model's predictions
filter_all = filter.volatility and filter.regime and filter.adx

// Filtered Signal: The model's prediction of future price movement direction with user-defined filters applied
signal := prediction > 0 and filter_all ? direction.long : prediction < 0 and filter_all ? direction.short : nz(signal[1])

// Bar-Count Filters: Represents strict filters based on a user-defined holding period
var int barsHeld = 0
barsHeld := ta.change(signal) ? 0 : barsHeld + 1
isHeldTargetBars = barsHeld == settings.exitAfterBars
isHeldLessThanTargetBars = 0 < barsHeld and barsHeld < settings.exitAfterBars

// Fractal Filters: Derived from relative appearances of signals in a given time series fractal/segment with a user-defined length
isDifferentSignalType = ta.change(signal)
// Check for early signal flips within the target holding period
isEarlySignalFlip = false
for i = 1 to settings.exitAfterBars - 1
    if ta.change(signal[i])
        isEarlySignalFlip := true
        break
isBuySignal = signal == direction.long and isEmaUptrend and isSmaUptrend
isSellSignal = signal == direction.short and isEmaDowntrend and isSmaDowntrend
isLastSignalBuy = signal[settings.exitAfterBars] == direction.long and isEmaUptrend[settings.exitAfterBars] and isSmaUptrend[settings.exitAfterBars]
isLastSignalSell = signal[settings.exitAfterBars] == direction.short and isEmaDowntrend[settings.exitAfterBars] and isSmaDowntrend[settings.exitAfterBars]
isNewBuySignal = isBuySignal and isDifferentSignalType
isNewSellSignal = isSellSignal and isDifferentSignalType

// Kernel Regression Filters: Filters based on Nadaraya-Watson Kernel Regression using the Rational Quadratic Kernel
// For more information on this technique refer to my other open source indicator located here: 
// https://www.tradingview.com/script/AWNvbPRM-Nadaraya-Watson-Rational-Quadratic-Kernel-Non-Repainting/
c_green = color.new(#009988, 20)
c_red = color.new(#CC3311, 20)
transparent = color.new(#000000, 100)
yhat1 = kernels.rationalQuadratic(settings.source, h, r, x)
yhat2 = kernels.gaussian(settings.source, h-lag, x)
kernelEstimate = yhat1
// Kernel Rates of Change
bool wasBearishRate = yhat1[2] > yhat1[1]
bool wasBullishRate = yhat1[2] < yhat1[1]
bool isBearishRate = yhat1[1] > yhat1
bool isBullishRate = yhat1[1] < yhat1
isBearishChange = isBearishRate and wasBullishRate
isBullishChange = isBullishRate and wasBearishRate
// Kernel Crossovers
bool isBullishCrossAlert = ta.crossover(yhat2, yhat1)
bool isBearishCrossAlert = ta.crossunder(yhat2, yhat1) 
bool isBullishSmooth = yhat2 >= yhat1
bool isBearishSmooth = yhat2 <= yhat1
// Kernel Colors
color colorByCross = isBullishSmooth ? c_green : c_red
color colorByRate = isBullishRate ? c_green : c_red
color plotColor = showKernelEstimate ? (useKernelSmoothing ? colorByCross : colorByRate) : transparent
plot(kernelEstimate, color=plotColor, linewidth=2, title="Kernel Regression Estimate")
// Alert Variables
bool alertBullish = useKernelSmoothing ? isBullishCrossAlert : isBullishChange
bool alertBearish = useKernelSmoothing ? isBearishCrossAlert : isBearishChange
// Bullish and Bearish Filters based on Kernel
isBullish = useKernelFilter ? (useKernelSmoothing ? isBullishSmooth : isBullishRate) : true
isBearish = useKernelFilter ? (useKernelSmoothing ? isBearishSmooth : isBearishRate) : true

// ===========================
// ==== Entries and Exits ====
// ===========================

// Entry Conditions: Booleans for ML Model Position Entries (Raw Signals)
startLongTradeRaw = isNewBuySignal and isBullish and isEmaUptrend and isSmaUptrend
startShortTradeRaw = isNewSellSignal and isBearish and isEmaDowntrend and isSmaDowntrend

// Trade State Tracking Variables
var int currentPosition = 0 // 0 = no position, 1 = long, -1 = short
var float entryPrice = 0.0
var int entryBar = 0
var float totalPoints = 0.0
var int tradeCount = 0

// Same-Side Signal Filtering: Only allow one entry per direction until exit
startLongTrade = startLongTradeRaw and currentPosition != 1
startShortTrade = startShortTradeRaw and currentPosition != -1

// Dynamic Exit Conditions: Booleans for ML Model Position Exits based on Fractal Filters and Kernel Regression Filters
lastSignalWasBullish = ta.barssince(startLongTrade) < ta.barssince(startShortTrade)
lastSignalWasBearish = ta.barssince(startShortTrade) < ta.barssince(startLongTrade)
barsSinceRedEntry = ta.barssince(startShortTrade)
barsSinceRedExit = ta.barssince(alertBullish)
barsSinceGreenEntry = ta.barssince(startLongTrade)
barsSinceGreenExit = ta.barssince(alertBearish)
isValidShortExit = barsSinceRedExit > barsSinceRedEntry
isValidLongExit = barsSinceGreenExit > barsSinceGreenEntry
endLongTradeDynamic = (isBearishChange and isValidLongExit[1])
endShortTradeDynamic = (isBullishChange and isValidShortExit[1])

// Fixed Exit Conditions: Booleans for ML Model Position Exits based on a Bar-Count Filters
endLongTradeStrict = ((isHeldTargetBars and isLastSignalBuy and settings.exitOnNeutral) or (isHeldLessThanTargetBars and isNewSellSignal and isLastSignalBuy)) and startLongTrade[settings.exitAfterBars]
endShortTradeStrict = ((isHeldTargetBars and isLastSignalSell and settings.exitOnNeutral) or (isHeldLessThanTargetBars and isNewBuySignal and isLastSignalSell)) and startShortTrade[settings.exitAfterBars]
isDynamicExitValid = not useEmaFilter and not useSmaFilter and not useKernelSmoothing
endLongTrade = settings.useDynamicExits and isDynamicExitValid ? endLongTradeDynamic : endLongTradeStrict 
endShortTrade = settings.useDynamicExits and isDynamicExitValid ? endShortTradeDynamic : endShortTradeStrict

// Trade Reversal Logic: Detect opposite signals while in trade
reverseToShort = startShortTradeRaw and currentPosition == 1
reverseToLong = startLongTradeRaw and currentPosition == -1

// Enhanced Exit Conditions with Reversals
actualEndLongTrade = endLongTrade or reverseToShort
actualEndShortTrade = endShortTrade or reverseToLong

// Update Position State and Calculate Points
if startLongTrade and currentPosition == 0
    currentPosition := 1
    entryPrice := close
    entryBar := bar_index
    tradeCount := tradeCount + 1
    
if startShortTrade and currentPosition == 0
    currentPosition := -1
    entryPrice := close
    entryBar := bar_index
    tradeCount := tradeCount + 1

if actualEndLongTrade and currentPosition == 1
    float exitPrice = close
    float points = exitPrice - entryPrice
    totalPoints := totalPoints + points
    currentPosition := 0
    // If this is a reversal, immediately enter short
    if reverseToShort
        currentPosition := -1
        entryPrice := close
        entryBar := bar_index
        tradeCount := tradeCount + 1

if actualEndShortTrade and currentPosition == -1
    float exitPrice = close
    float points = entryPrice - exitPrice
    totalPoints := totalPoints + points
    currentPosition := 0
    // If this is a reversal, immediately enter long
    if reverseToLong
        currentPosition := 1
        entryPrice := close
        entryBar := bar_index
        tradeCount := tradeCount + 1

// =========================
// ==== Plotting Labels ====
// =========================

// Note: Labels positioned above/below candles for better visibility
// ATR-based offset for label positioning
labelOffset = ta.atr(14) * 0.5

// Entry Labels
if startLongTrade
    label.new(bar_index, low - labelOffset, "Buy", xloc.bar_index, yloc.price, color=color.new(color.green, 0), textcolor=color.white, style=label.style_label_up, size=size.normal)

if startShortTrade
    label.new(bar_index, high + labelOffset, "Sell", xloc.bar_index, yloc.price, color=color.new(color.red, 0), textcolor=color.white, style=label.style_label_down, size=size.normal)

// Exit Labels
if actualEndLongTrade and settings.showExits
    exitLabel = reverseToShort ? "TP" : "SL"
    label.new(bar_index, high + labelOffset, exitLabel, xloc.bar_index, yloc.price, color=color.new(color.orange, 0), textcolor=color.white, style=label.style_label_down, size=size.small)

if actualEndShortTrade and settings.showExits
    exitLabel = reverseToLong ? "TP" : "SL"
    label.new(bar_index, low - labelOffset, exitLabel, xloc.bar_index, yloc.price, color=color.new(color.orange, 0), textcolor=color.white, style=label.style_label_up, size=size.small)

// ================
// ==== Alerts ====
// ================ 

// Separate Alerts for Entries and Exits
alertcondition(startLongTrade, title='Open Long ▲', message='LDC Open Long ▲ | {{ticker}}@{{close}} | ({{interval}})')
alertcondition(actualEndLongTrade, title='Close Long ▲', message='LDC Close Long ▲ | {{ticker}}@{{close}} | ({{interval}})')
alertcondition(startShortTrade, title='Open Short ▼', message='LDC Open Short  | {{ticker}}@{{close}} | ({{interval}})')
alertcondition(actualEndShortTrade, title='Close Short ▼', message='LDC Close Short ▼ | {{ticker}}@{{close}} | ({{interval}})')

// Combined Alerts for Entries and Exits
alertcondition(startShortTrade or startLongTrade, title='Open Position ▲▼', message='LDC Open Position ▲▼ | {{ticker}}@{{close}} | ({{interval}})')
alertcondition(actualEndShortTrade or actualEndLongTrade, title='Close Position ▲▼', message='LDC Close Position  ▲▼ | {{ticker}}@[{{close}}] | ({{interval}})')

// Kernel Estimate Alerts
alertcondition(condition=alertBullish, title='Kernel Bullish Color Change', message='LDC Kernel Bullish ▲ | {{ticker}}@{{close}} | ({{interval}})')
alertcondition(condition=alertBearish, title='Kernel Bearish Color Change', message='LDC Kernel Bearish ▼ | {{ticker}}@{{close}} | ({{interval}})')

// =========================
// ==== Display Signals ==== 
// =========================

atrSpaced = useAtrOffset ? ta.atr(1) : na
compressionFactor = settings.neighborsCount / settings.colorCompression
c_pred = prediction > 0 ? color.from_gradient(prediction, 0, compressionFactor, #787b86, #009988) : prediction <= 0 ? color.from_gradient(prediction, -compressionFactor, 0, #CC3311, #787b86) : na
c_label = showBarPredictions ? c_pred : na
c_bars = showBarColors ? color.new(c_pred, 50) : na
x_val = bar_index
y_val = useAtrOffset ? prediction > 0 ? high + atrSpaced: low - atrSpaced : prediction > 0 ? high + hl2*barPredictionsOffset/20 : low - hl2*barPredictionsOffset/30
label.new(x_val, y_val, str.tostring(prediction), xloc.bar_index, yloc.price, color.new(color.white, 100), label.style_label_up, c_label, size.normal, text.align_left)
barcolor(showBarColors ? color.new(c_pred, 50) : na)

// ===================== 
// ==== Backtesting ====
// =====================

// The following can be used to stream signals to a backtest adapter
backTestStream = switch 
    startLongTrade => 1
    actualEndLongTrade => 2
    startShortTrade => -1
    actualEndShortTrade => -2
plot(backTestStream, "Backtest Stream", display=display.none)

// The following can be used to display real-time trade stats. This can be a useful mechanism for obtaining real-time feedback during Feature Engineering. This does NOT replace the need to properly backtest.
// Note: In this context, a "Stop-Loss" is defined instances where the ML Signal prematurely flips directions before an exit signal can be generated.
[totalWins, totalLosses, totalEarlySignalFlips, totalTrades, tradeStatsHeader, winLossRatio, winRate] = ml.backtest(high, low, open, startLongTrade, actualEndLongTrade, startShortTrade, actualEndShortTrade, isEarlySignalFlip, maxBarsBackIndex, bar_index, settings.source, useWorstCase)

init_table() =>
    c_transparent = color.new(color.black, 100)
    table.new(position.top_right, columns=2, rows=7, frame_color=color.new(color.black, 100), frame_width=1, border_width=1, border_color=c_transparent)

update_table(tbl, tradeStatsHeader, totalTrades, totalWins, totalLosses, winLossRatio, winRate, stopLosses) => 
    c_transparent = color.new(color.black, 100)
    table.cell(tbl, 0, 0, tradeStatsHeader, text_halign=text.align_center, text_color=color.gray, text_size=size.normal)
    table.cell(tbl, 0, 1, 'Winrate', text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.normal)
    table.cell(tbl, 1, 1, str.tostring(totalWins / totalTrades, '#.#%'), text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.normal)
    table.cell(tbl, 0, 2, 'Trades', text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.normal)
    table.cell(tbl, 1, 2, str.tostring(totalTrades, '#') + ' (' + str.tostring(totalWins, '#') + '|' + str.tostring(totalLosses, '#') + ')', text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.normal)
    table.cell(tbl, 0, 5, 'WL Ratio', text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.normal)
    table.cell(tbl, 1, 5, str.tostring(totalWins / totalLosses, '0.00'), text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.normal)
    table.cell(tbl, 0, 6, 'Early Signal Flips', text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.normal)
    table.cell(tbl, 1, 6, str.tostring(totalEarlySignalFlips, '#'), text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.normal)

if showTradeStats
    var tbl = ml.init_table()
    if barstate.islast
        update_table(tbl, tradeStatsHeader, totalTrades, totalWins, totalLosses, winLossRatio, winRate, totalEarlySignalFlips)

// ======================================
// ==== Enhanced Performance Tables ====
// ======================================

// Input for showing enhanced performance tables
showPerformanceTable = input.bool(true, "Show Performance Table", tooltip="Displays a comprehensive performance table with detailed trade statistics.", group="Display Settings")
showHourlyTable = input.bool(false, "Show Hourly Analysis Table", tooltip="Displays hourly win rates, profit factors, and PNL breakdown.", group="Display Settings")

// Enhanced Trade Tracking with detailed metrics
var array<float> tradePoints = array.new_float()
var array<int> tradeDurations = array.new_int()
var array<int> tradeTypes = array.new_int() // 1 = long, -1 = short
var array<int> tradeHours = array.new_int()
var array<float> tradeWins = array.new_float()
var array<float> tradeLosses = array.new_float()
var float maxDrawdown = 0.0
var float peakEquity = 0.0
var float currentEquity = 0.0
var int consecutiveWins = 0
var int consecutiveLosses = 0
var int maxConsecutiveWins = 0
var int maxConsecutiveLosses = 0

// Track trade completion for statistics
var bool lastWasLong = false
var bool lastWasShort = false
var float lastEntryPrice = 0.0
var int lastEntryBar = 0

if startLongTrade
    lastWasLong := true
    lastWasShort := false
    lastEntryPrice := close
    lastEntryBar := bar_index

if startShortTrade
    lastWasShort := true
    lastWasLong := false
    lastEntryPrice := close
    lastEntryBar := bar_index

if actualEndLongTrade and lastWasLong
    float points = close - lastEntryPrice
    int duration = bar_index - lastEntryBar
    array.push(tradePoints, points)
    array.push(tradeDurations, duration)
    array.push(tradeTypes, 1)
    array.push(tradeHours, hour(time))
    currentEquity := currentEquity + points
    if points > 0
        array.push(tradeWins, points)
        consecutiveWins := consecutiveWins + 1
        consecutiveLosses := 0
        maxConsecutiveWins := math.max(maxConsecutiveWins, consecutiveWins)
    else
        array.push(tradeLosses, math.abs(points))
        consecutiveLosses := consecutiveLosses + 1
        consecutiveWins := 0
        maxConsecutiveLosses := math.max(maxConsecutiveLosses, consecutiveLosses)
    if currentEquity > peakEquity
        peakEquity := currentEquity
    float drawdown = peakEquity - currentEquity
    if drawdown > maxDrawdown
        maxDrawdown := drawdown
    lastWasLong := false

if actualEndShortTrade and lastWasShort
    float points = lastEntryPrice - close
    int duration = bar_index - lastEntryBar
    array.push(tradePoints, points)
    array.push(tradeDurations, duration)
    array.push(tradeTypes, -1)
    array.push(tradeHours, hour(time))
    currentEquity := currentEquity + points
    if points > 0
        array.push(tradeWins, points)
        consecutiveWins := consecutiveWins + 1
        consecutiveLosses := 0
        maxConsecutiveWins := math.max(maxConsecutiveWins, consecutiveWins)
    else
        array.push(tradeLosses, math.abs(points))
        consecutiveLosses := consecutiveLosses + 1
        consecutiveWins := 0
        maxConsecutiveLosses := math.max(maxConsecutiveLosses, consecutiveLosses)
    if currentEquity > peakEquity
        peakEquity := currentEquity
    float drawdown = peakEquity - currentEquity
    if drawdown > maxDrawdown
        maxDrawdown := drawdown
    lastWasShort := false

// Calculate comprehensive statistics
calcWinRate() =>
    int wins = 0
    int total = array.size(tradePoints)
    if total > 0
        for i = 0 to total - 1
            if array.get(tradePoints, i) > 0
                wins := wins + 1
        wins / total
    else
        0.0

calcProfitFactor() =>
    float grossProfit = array.size(tradeWins) > 0 ? array.sum(tradeWins) : 0.0
    float grossLoss = array.size(tradeLosses) > 0 ? array.sum(tradeLosses) : 0.0
    grossLoss > 0 ? grossProfit / grossLoss : grossProfit > 0 ? 999.0 : 0.0

calcAvgWin() =>
    array.size(tradeWins) > 0 ? array.avg(tradeWins) : 0.0

calcAvgLoss() =>
    array.size(tradeLosses) > 0 ? array.avg(tradeLosses) : 0.0

calcExpectancy() =>
    float wr = calcWinRate()
    float avgWin = calcAvgWin()
    float avgLoss = calcAvgLoss()
    (wr * avgWin) - ((1 - wr) * avgLoss)

// Initialize comprehensive performance table
init_performance_table() =>
    c_transparent = color.new(color.black, 100)
    table.new(position.bottom_right, columns=2, rows=14, frame_color=color.new(color.black, 100), frame_width=1, border_width=1, border_color=c_transparent)

// Update comprehensive performance table
update_performance_table(tbl) =>
    c_transparent = color.new(color.black, 100)
    c_header = color.new(#1e222d, 80)
    
    int totalTrades = array.size(tradePoints)
    float winRate = calcWinRate()
    float profitFactor = calcProfitFactor()
    float avgWin = calcAvgWin()
    float avgLoss = calcAvgLoss()
    float expectancy = calcExpectancy()
    float totalPnl = array.size(tradePoints) > 0 ? array.sum(tradePoints) : 0.0
    int numWins = array.size(tradeWins)
    int numLosses = array.size(tradeLosses)
    float avgDuration = array.size(tradeDurations) > 0 ? array.avg(tradeDurations) : 0.0
    
    table.cell(tbl, 0, 0, 'Performance Metrics', text_halign=text.align_center, bgcolor=c_header, text_color=color.white, text_size=size.normal)
    table.cell(tbl, 1, 0, '', text_halign=text.align_center, bgcolor=c_header, text_color=color.white, text_size=size.normal)
    
    table.cell(tbl, 0, 1, 'Total Trades', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 1, str.tostring(totalTrades), text_halign=text.align_right, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    
    table.cell(tbl, 0, 2, 'Win Rate', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 2, str.tostring(winRate * 100, '0.00') + '%', text_halign=text.align_right, bgcolor=c_transparent, text_color=winRate >= 0.5 ? color.green : color.red, text_size=size.small)
    
    table.cell(tbl, 0, 3, 'Profit Factor', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 3, str.tostring(profitFactor, '0.00'), text_halign=text.align_right, bgcolor=c_transparent, text_color=profitFactor >= 1.5 ? color.green : color.red, text_size=size.small)
    
    table.cell(tbl, 0, 4, 'Total PNL', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 4, str.tostring(totalPnl, '0.00'), text_halign=text.align_right, bgcolor=c_transparent, text_color=totalPnl > 0 ? color.green : color.red, text_size=size.small)
    
    table.cell(tbl, 0, 5, 'Avg Win', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 5, str.tostring(avgWin, '0.00'), text_halign=text.align_right, bgcolor=c_transparent, text_color=color.green, text_size=size.small)
    
    table.cell(tbl, 0, 6, 'Avg Loss', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 6, str.tostring(avgLoss, '0.00'), text_halign=text.align_right, bgcolor=c_transparent, text_color=color.red, text_size=size.small)
    
    table.cell(tbl, 0, 7, 'Expectancy', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 7, str.tostring(expectancy, '0.00'), text_halign=text.align_right, bgcolor=c_transparent, text_color=expectancy > 0 ? color.green : color.red, text_size=size.small)
    
    table.cell(tbl, 0, 8, 'Max Drawdown', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 8, str.tostring(maxDrawdown, '0.00'), text_halign=text.align_right, bgcolor=c_transparent, text_color=color.red, text_size=size.small)
    
    table.cell(tbl, 0, 9, 'Wins / Losses', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 9, str.tostring(numWins) + ' / ' + str.tostring(numLosses), text_halign=text.align_right, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    
    table.cell(tbl, 0, 10, 'Avg Duration', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 10, str.tostring(avgDuration, '0.0') + ' bars', text_halign=text.align_right, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    
    table.cell(tbl, 0, 11, 'Max Win Streak', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 11, str.tostring(maxConsecutiveWins), text_halign=text.align_right, bgcolor=c_transparent, text_color=color.green, text_size=size.small)
    
    table.cell(tbl, 0, 12, 'Max Loss Streak', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 12, str.tostring(maxConsecutiveLosses), text_halign=text.align_right, bgcolor=c_transparent, text_color=color.red, text_size=size.small)
    
    table.cell(tbl, 0, 13, 'Current Equity', text_halign=text.align_left, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
    table.cell(tbl, 1, 13, str.tostring(currentEquity, '0.00'), text_halign=text.align_right, bgcolor=c_transparent, text_color=currentEquity > 0 ? color.green : color.red, text_size=size.small)

// Initialize hourly analysis table
init_hourly_table() =>
    c_transparent = color.new(color.black, 100)
    table.new(position.bottom_left, columns=4, rows=25, frame_color=color.new(color.black, 100), frame_width=1, border_width=1, border_color=c_transparent)

// Calculate hourly statistics
calcHourlyStats(h) =>
    var array<float> hourlyPoints = array.new_float()
    array.clear(hourlyPoints)
    
    for i = 0 to array.size(tradeHours) - 1
        if array.get(tradeHours, i) == h
            array.push(hourlyPoints, array.get(tradePoints, i))
    
    int hourlyTrades = array.size(hourlyPoints)
    float hourlyWR = 0.0
    float hourlyPF = 0.0
    float hourlyPNL = 0.0
    
    if hourlyTrades > 0
        hourlyPNL := array.sum(hourlyPoints)
        int hourlyWins = 0
        float grossProfit = 0.0
        float grossLoss = 0.0
        
        for i = 0 to hourlyTrades - 1
            float points = array.get(hourlyPoints, i)
            if points > 0
                hourlyWins := hourlyWins + 1
                grossProfit := grossProfit + points
            else
                grossLoss := grossLoss + math.abs(points)
        
        hourlyWR := hourlyWins / hourlyTrades
        hourlyPF := grossLoss > 0 ? grossProfit / grossLoss : grossProfit > 0 ? 999.0 : 0.0
    
    [hourlyWR, hourlyPF, hourlyPNL, hourlyTrades]

// Update hourly analysis table
update_hourly_table(tbl) =>
    c_transparent = color.new(color.black, 100)
    c_header = color.new(#1e222d, 80)
    
    table.cell(tbl, 0, 0, 'Hour', text_halign=text.align_center, bgcolor=c_header, text_color=color.white, text_size=size.small)
    table.cell(tbl, 1, 0, 'WR %', text_halign=text.align_center, bgcolor=c_header, text_color=color.white, text_size=size.small)
    table.cell(tbl, 2, 0, 'PF', text_halign=text.align_center, bgcolor=c_header, text_color=color.white, text_size=size.small)
    table.cell(tbl, 3, 0, 'PNL', text_halign=text.align_center, bgcolor=c_header, text_color=color.white, text_size=size.small)
    
    for h = 0 to 23
        [wr, pf, pnl, trades] = calcHourlyStats(h)
        row = h + 1
        
        table.cell(tbl, 0, row, str.format('{0,number,00}:00', h), text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
        
        if trades > 0
            table.cell(tbl, 1, row, str.tostring(wr * 100, '0.0'), text_halign=text.align_center, bgcolor=c_transparent, text_color=wr >= 0.5 ? color.green : color.red, text_size=size.small)
            table.cell(tbl, 2, row, str.tostring(pf, '0.00'), text_halign=text.align_center, bgcolor=c_transparent, text_color=pf >= 1.5 ? color.green : color.red, text_size=size.small)
            table.cell(tbl, 3, row, str.tostring(pnl, '0.00'), text_halign=text.align_center, bgcolor=c_transparent, text_color=pnl > 0 ? color.green : color.red, text_size=size.small)
        else
            table.cell(tbl, 1, row, '-', text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
            table.cell(tbl, 2, row, '-', text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)
            table.cell(tbl, 3, row, '-', text_halign=text.align_center, bgcolor=c_transparent, text_color=color.gray, text_size=size.small)

// Display tables on last bar
if showPerformanceTable and barstate.islast
    var perfTable = init_performance_table()
    update_performance_table(perfTable)

if showHourlyTable and barstate.islast
    var hourlyTable = init_hourly_table()
    update_hourly_table(hourlyTable)
